package regal.config

# METADATA
# description: |
#   determines if file should be excluded, either because of an override,
#   or because the specific rule configuration excludes it
#
#   imitates .gitignore pattern matching as best it can
#   ref: https://git-scm.com/docs/gitignore#_pattern_format
excluded_file(category, title, file) if {
	some compiled in data.internal.prepared.ignore_patterns.files[category][title]
	glob.match(compiled, ["/"], file)
}

# METADATA
# description: |
#   pattern_compiler transforms a glob pattern into a set of glob
#   patterns to make the combined set behave as .gitignore
patterns_compiler(patterns) := {_middle_doublestar(compiled) |
	some pattern in patterns
	some processed in _leading_doublestar_pattern(_internal_slashes(pattern))
	some compiled in _trailing_slash(processed)
} if {
	patterns != []
}

# Internal slashes means that the path is relative to root,
# if not it can appear anywhere in the hierarchy
#
# myfiledir and mydir/ turns into **/myfiledir and **/mydir/
# mydir/p and mydir/d/ are returned as is
_internal_slashes(pattern) := trim_prefix(pattern, "/") if {
	contains(trim_suffix(pattern, "/"), "/")
} else := $"**/{pattern}"

# **/pattern might match my/dir/pattern and pattern
# So we branch it into itself and one with the leading **/ removed
_leading_doublestar_pattern(pattern) := {pattern, substring(pattern, 3, -1)} if {
	startswith(pattern, "**/")
} else := {pattern}

# If a pattern does not end with a "/", then it can both
# - match a folder => pattern + "/**"
# - match a file => pattern
_trailing_slash(pattern) := {pattern, $"{pattern}/**"} if {
	not endswith(pattern, "/")
	not endswith(pattern, "**")
} else := {$"{pattern}**"} if {
	endswith(pattern, "/")
} else := {pattern}

# .gitignore has an interior "/**/" match zero directories as well as one or
# more, while glob's ** only matches a non-empty sequence, so "a/**/b" alone
# won't match "a/b". An empty alternative restores it, for any number of
# occurrences.
#
# Keep in sync with `middleDoubleStar` in `pkg/config/filter.go`.
_middle_doublestar(pattern) := replace(pattern, "/**/", "/{**/,}")
